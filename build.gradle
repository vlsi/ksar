buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'checkstyle'

group = 'com.github.vlsi.ksar'
archivesBaseName = 'ksar'
version = '5.2.4-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = 'net.atomique.ksar.Main'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'

    compile group: 'com.itextpdf', name: 'itextpdf', version: '5.5.13'
    compile group: 'com.jcraft', name: 'jsch', version: '0.1.54'
    compile group: 'org.jfree', name: 'jfreechart', version: '1.5.0'
    testCompile 'junit:junit:4.12'
}

task writeVersion << {
    def s = version.find(/^\d.\d.\d/)
    if (s == null) {
        throw new InvalidUserDataException('Invalid version format')
    }
    new File("build/resources/main/kSar.version").text = s
}
classes.dependsOn(writeVersion)

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

checkstyle {
    configFile = new File(rootDir, "/src/main/checkstyle/postgressql-checks.xml")
}

jar {
    manifest {
        attributes("Implementation-Title": "Gradle",
                "Implementation-Version": version,
                "SplashScreen-Image": "logo_ksar.jpg")
    }
}
